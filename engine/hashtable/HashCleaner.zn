library HashCleaner requires Stuns, Auras, Buffs, Combat {

	// Событие при очистке хэш-таблицы для юнита
	//
	// GetTriggerHandleId() -> integer
	//! @gui
	public integer EVENT_CLEAR_HASH;

	integer TriggerHandleId;

	//! @gui
	public function GetTriggerHandleId() -> integer { debug DebugEvent.a(EVENT_CLEAR_HASH).run(); return TriggerHandleId; }
	

	// TO DO
	struct ClearUnit {
		unit u;
		integer handleId;
		timer tm;
		integer count;
		static integer CK_HID = 0;
	
		static method create(unit u) -> ClearUnit {
			ClearUnit this = ClearUnit.allocate();
			this.u = u;
			this.handleId = GetHandleId(u);
			this.tm = CreateTimer();
			this.count = 0;
			if (IsUnitDummy(u)) {
				this.clear();
				return 0;
			}

			SaveInteger(HT, GetHandleId(this.tm), CK_HID, this);
			TimerStart(this.tm, 1, true, function ClearUnit.check);

			RemoveUnitStun(u);

			return this;
		}

		method destroy() {
			PauseTimer(tm);
			DestroyTimer(tm);
			tm = null;
			u = null;
			this.deallocate();
		}

		method clear() {
			TriggerHandleId = handleId;
			ExecuteEvent(EVENT_CLEAR_HASH);

			DestroyAllAurasByHashId(handleId);
			DestroyAllBuffsByHashId(handleId);
			DestroyAllModifiersByHashId(handleId);
			DestroyAllMultiModifiersByHashId(handleId);
			if (IsSet(UnitData.load(handleId))) UnitData.load(handleId).destroy();
			if (IsSet(Regeneration.load(handleId))) Regeneration.load(handleId).destroy();
			if (IsSet(MoveSpeed.load(handleId))) MoveSpeed.load(handleId).destroy();
			if (IsSet(Combat.load(handleId))) Combat.load(handleId).destroy();
			if (IsSet(Fatigue.load(handleId))) Fatigue.load(handleId).destroy();
			
			FlushChildHashtable(HT, handleId);
			FlushChildHashtable(udg_HT_GUI, handleId);
			this.destroy();
		}

		static method check() {
			ClearUnit this = LoadInteger(HT, GetHandleId(GetExpiredTimer()), CK_HID);
			count = count + 1;
			if (GetUnitTypeId(u) == 0 && !UnitAlive(u)) {
				this.clear();
			} else if (count >= 100 || UnitAlive(u)) {
				this.destroy();
			}
		}
		
	}

	function onInit() {
		EVENT_CLEAR_HASH = CreateEvent("EVENT_CLEAR_HASH");
		EventRegisterAction(GetHandleId(EVENT_PLAYER_UNIT_DEATH), function() {
			ClearUnit.create(GetTriggerUnit());
		});
	}

}
