library HealLifestealRestore requires Events {

// Variables

	public integer EVENT_HEAL_MODIFIER;

	public integer EVENT_HEAL;

	public integer EVENT_LIFESTEAL_MODIFIER;

	public integer EVENT_LIFESTEAL;

	public integer EVENT_RESTORE_MODIFIER;

	public integer EVENT_RESTORE;

	SummativeText TempText;

	// Debug Variables
	public boolean DebugHeal = false;

	// Getters Variables
	real HealAmount = 0.0;
	unit HealedUnit = null;
	unit HealingUnit = null;
	real OverHeal = 0.0;

	real LifestealAmount = 0.0;
	unit LifestealedUnit = null;
	real OverLifesteal = 0.0;

	real RestoredAmount = 0.0;
	unit RestoredUnit = null;
	unit RestoringUnit = null;
	real OverRestore = 0.0;

// Tech Functions

	function dbg(string msg) {
		if (DebugHeal) Debug(msg);
	}

// API

	// Event Getters

	public function GetHealAmount() -> real {
		debug DebugEvent.a(EVENT_HEAL_MODIFIER).a(EVENT_HEAL).run();
		return HealAmount;
	}
	public function GetHealedUnit() -> unit {
		debug DebugEvent.a(EVENT_HEAL_MODIFIER).a(EVENT_HEAL).run();
		return HealedUnit;
	}
	public function GetHealingUnit() -> unit {
		debug DebugEvent.a(EVENT_HEAL_MODIFIER).a(EVENT_HEAL).run();
		return HealingUnit;
	}
	public function GetOverHeal() -> real {
		debug DebugEvent.a(EVENT_HEAL).run();
		return OverHeal;
	}

	public function GetLifestealAmount() -> real {
		debug DebugEvent.a(EVENT_LIFESTEAL_MODIFIER).a(EVENT_LIFESTEAL).run();
		return LifestealAmount;
	}
	public function GetLifestealedUnit() -> unit {
		debug DebugEvent.a(EVENT_LIFESTEAL_MODIFIER).a(EVENT_LIFESTEAL).run();
		return LifestealedUnit;
	}
	public function GetOverLifesteal() -> real {
		debug DebugEvent.a(EVENT_LIFESTEAL).run();
		return OverLifesteal;
	}

	public function GetRestoredAmount() -> real {
		debug DebugEvent.a(EVENT_RESTORE_MODIFIER).a(EVENT_RESTORE).run();
		return RestoredAmount;
	}
	public function GetRestoredUnit() -> unit {
		debug DebugEvent.a(EVENT_RESTORE_MODIFIER).a(EVENT_RESTORE).run();
		return RestoredUnit;
	}
	public function GetRestoringUnit() -> unit {
		debug DebugEvent.a(EVENT_RESTORE_MODIFIER).a(EVENT_RESTORE).run();
		return RestoringUnit;
	}
	public function GetOverRestore() -> real {
		debug DebugEvent.a(EVENT_RESTORE).run();
		return OverRestore;
	}

	// Event Setters

	public function SetHealAmount(real healAmount) {
		debug DebugEvent.a(EVENT_HEAL_MODIFIER).run();
		HealAmount = healAmount;
	}
	public function SetLifestealAmount(real lifestealAmount) {
		debug DebugEvent.a(EVENT_LIFESTEAL_MODIFIER).run();
		LifestealAmount = lifestealAmount;
	}
	public function SetRestoredAmount(real restoredAmount) {
		debug DebugEvent.a(EVENT_RESTORE_MODIFIER).run();
		RestoredAmount = restoredAmount;
	}

	// Functions

	// Лечит юнита с возможностью модификации значения через EVENT_HEAL_MODIFIER
	// Если итоговое значение положительное - применяет лечение и вызывает EVENT_HEAL
	// @arg unit healingUnit - юнит, выполняющий лечение
	// @arg unit healedUnit - юнит, получающий лечение
	// @arg real amount - базовое значение лечения
	public function HealUnit(unit healingUnit, unit healedUnit, real amount) {
		HealAmount = amount;
		HealedUnit = healedUnit;
		HealingUnit = healingUnit;
		OverHeal = 0.0;

		ExecuteEvent(EVENT_HEAL_MODIFIER);

		if (HealAmount > 0.0) {
			OverHeal = MaxReal(0, HealAmount - (GetUnitMaxLife(HealedUnit) - GetUnitState(HealedUnit, UNIT_STATE_LIFE)));
			SetUnitState(HealedUnit, UNIT_STATE_LIFE, GetUnitState(HealedUnit, UNIT_STATE_LIFE) + HealAmount);
			ExecuteEvent(EVENT_HEAL);
		}

		HealAmount = 0.0;
		HealedUnit = null;
		HealingUnit = null;
		OverHeal = 0.0;
	}

	// Применяет лайфстил к юниту с возможностью модификации значения через EVENT_LIFESTEAL_MODIFIER
	// Если итоговое значение положительное - восстанавливает здоровье и вызывает EVENT_LIFESTEAL
	// @arg unit whichUnit - юнит, получающий лайфстил
	// @arg real amount - базовое значение лайфстила
	public function LifestealUnit(unit whichUnit, real amount) {
		LifestealAmount = amount;
		LifestealedUnit = whichUnit;
		OverLifesteal = 0.0;

		ExecuteEvent(EVENT_LIFESTEAL_MODIFIER);

		if (LifestealAmount > 0.0) {
			OverLifesteal = MaxReal(0, LifestealAmount - (GetUnitMaxLife(LifestealedUnit) - GetUnitState(LifestealedUnit, UNIT_STATE_LIFE)));
			SetUnitState(LifestealedUnit, UNIT_STATE_LIFE, GetUnitState(LifestealedUnit, UNIT_STATE_LIFE) + LifestealAmount);
			ExecuteEvent(EVENT_LIFESTEAL);
		}

		LifestealAmount = 0.0;
		LifestealedUnit = null;
		OverLifesteal = 0.0;
	}

	// Восстанавливает ману юниту с возможностью модификации значения через EVENT_RESTORE_MODIFIER
	// Если итоговое значение положительное - восстанавливает ману и вызывает EVENT_RESTORE
	// @arg unit restoringUnit - юнит, выполняющий восстановление
	// @arg unit restoredUnit - юнит, получающий восстановление маны
	// @arg real amount - базовое значение восстановления маны
	public function RestoreManaUnit(unit restoringUnit, unit restoredUnit, real amount) {
		RestoredAmount = amount;
		RestoredUnit = restoredUnit;
		RestoringUnit = restoringUnit;
		OverRestore = 0.0;

		ExecuteEvent(EVENT_RESTORE_MODIFIER);

		if (RestoredAmount > 0.0) {
			OverRestore = MaxReal(0, RestoredAmount - (GetUnitState(RestoredUnit, UNIT_STATE_MAX_MANA) - GetUnitState(RestoredUnit, UNIT_STATE_MANA)));
			SetUnitState(RestoredUnit, UNIT_STATE_MANA, GetUnitState(RestoredUnit, UNIT_STATE_MANA) + RestoredAmount);
			ExecuteEvent(EVENT_RESTORE);
		}

		RestoredAmount = 0.0;
		RestoredUnit = null;
		RestoringUnit = null;
		OverRestore = 0.0;
	}

	function onInit() {
		EVENT_HEAL_MODIFIER = CreateEvent("EVENT_HEAL_MODIFIER");
		EVENT_HEAL = CreateEvent("EVENT_HEAL");
		EVENT_LIFESTEAL_MODIFIER = CreateEvent("EVENT_LIFESTEAL_MODIFIER");
		EVENT_LIFESTEAL = CreateEvent("EVENT_LIFESTEAL");
		EVENT_RESTORE_MODIFIER = CreateEvent("EVENT_RESTORE_MODIFIER");
		EVENT_RESTORE = CreateEvent("EVENT_RESTORE");

		RegisterAction(EVENT_HEAL, function() {
			TempText = AddSummativeTextToUnitForPlayer(GetOwningPlayer(GetHealingUnit()), GetHealedUnit(), HEAL_TEXT, GetHealAmount(), "");
			TempText.showToPlayer(GetOwningPlayer(GetHealedUnit()));
			if (IsMapDuel())
				TempText.showToObservers();
		});

		RegisterAction(EVENT_LIFESTEAL, function() {
			TempText = AddSummativeTextToUnitForPlayer(GetOwningPlayer(GetLifestealedUnit()), GetLifestealedUnit(), HEAL_TEXT, GetLifestealAmount(), "");
			if (IsMapDuel())
				TempText.showToObservers();
			DestroyEffect(AddSpecialEffectTarget("Abilities\\Spells\\Undead\\VampiricAura\\VampiricAuraTarget.mdx", GetLifestealedUnit(), "origin"));
		});

		RegisterAction(EVENT_RESTORE, function() {
			TempText = AddSummativeTextToUnitForPlayer(GetOwningPlayer(GetRestoredUnit()), GetRestoredUnit(), MANA_TEXT, GetRestoredAmount(), "");
			if (IsMapDuel())
				TempText.showToObservers();
		});
	}

}

