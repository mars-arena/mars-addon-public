library Nomi initializer onInit uses Filters
function RandomSwap takes unit caster returns nothing
    local group swapGroup = CreateGroup()
    local unit target
    local real range = 600.00 // Задаем радиус действия способности
    local integer count = 0
    local unit array units
    local real array posX
    local real array posY
    local integer i = 0
    local integer j = 0

    // Собираем всех юнитов в области вокруг кастера
    call GroupEnumUnitsInRange(swapGroup, GetUnitX(caster), GetUnitY(caster), range, Condition(function FilterAllMoveable))
    loop
        set target = FirstOfGroup(swapGroup)
        exitwhen target == null
        set units[count] = target
        set posX[count] = GetUnitX(target)
        set posY[count] = GetUnitY(target)
        set count = count + 1
        call GroupRemoveUnit(swapGroup, target)
    endloop

    // Отключаем проходимость для каждого юнита
    set i = 0
    loop
        exitwhen i >= count
        set target = units[i]
        call SetUnitPathing(target, false)
        set i = i + 1
    endloop

    // Перемешиваем позиции юнитов
    set i = 0
    loop
        exitwhen i >= count
        set j = i + 1
        if j >= count then
            set j = 0
        endif
        if IsUnitEnemy(units[i], GetOwningPlayer(caster)) then
        call SetUnitPosition(units[i], posX[j], posY[j])
        else
        call SetUnitXY(units[i], posX[j], posY[j])
        endif
        set i = i + 1
    endloop

    // Включаем проходимость и наносим урон врагам
    set i = 0
    loop
        exitwhen i >= count
        set target = units[i]
        call DestroyEffect(AddSpecialEffectTarget("Abilities\\Spells\\Items\\AIil\\AIilTarget.mdl", target, "origin"))
        call SetUnitPathing(target, true)
        if IsUnitEnemy(target, GetOwningPlayer(caster)) then
            call AddBuffToUnit(caster, target, nomi5buff_tid, nomi5buff_tid, 3, 0, 0, false, false, false, false, 0,  false, false, false, false, 0)
        endif
        set i = i + 1
    endloop

    call DestroyGroup(swapGroup)
endfunction

private function spellAction takes nothing returns nothing
            if (GetSpellAbilityId() == 'A227' or GetSpellAbilityId() == 'A22E') then
                call RandomSwap(GetSpellAbilityUnit())
            endif
endfunction

private function onInit takes nothing returns nothing
    call RegisterAction(GetHandleId(EVENT_PLAYER_UNIT_SPELL_EFFECT), function spellAction)
endfunction 
endlibrary